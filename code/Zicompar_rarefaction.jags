model{

  for (i in 1:D[1]) { 			# Compure sum loglikelihood of esimated parameters for each OTU

	for (j in 1:group[1]){ 				# for each sample of grp 1

  			Y[i,j] ~ dpois(exp((phi[i,j]) * pi[i,j]))
  			pi[i,j] ~ dbern(p[i]) 					# zero inflation (switch on/off)
  			phi[i,j] ~ dnorm(alpha[i], binTau[i]) 			# true abundances means and random effects due to overdispertion (log scale)
  		}
  	  	  
     	for (j in (group[1]+1):(group[1]+group[2])){ 	# for each sample of grp 2

  			Y[i,j] ~ dpois(exp((phi[i,j])* pi[i,j])) 
  			pi[i,j] ~ dbern(p[i]*beta_p[i])					# zero inflation (switch on/off)
  			phi[i,j] ~ dnorm(alpha[i] + beta[i], binTau[i]) 	# true abundances mean, abudances mean differences and random effects due to overdispertion (log scale)

      		}
      
  	alpha[i] ~ dnorm(0,0.01) 			# OTU specific true abundance mean (log scale)
  	beta[i] ~ dnorm(0,0.01) 			# OTU diff abundance in grp 2 compaire to grp 1
  		
     	p[i] ~ dbeta(1,1) 				# Zero infaltion parameter specific to OTU (with beta prior distrib)
      	beta_p[i] ~ dbeta(1,1) 				# OTU diff detection in grp 2 compaire to grp 1 (0 no diff,1 full diff)

      	binVar[i] ~ dgamma(kappa,eta) T(0,10000)	# Gamma distribution on variance
	binTau[i] <- 1/binVar[i] 			# compute precision (inv. of variance)
  }
  
  # hyper-prior
  kappa ~ dunif(0,50) 
  eta ~ dunif(0,1000) 
	
}