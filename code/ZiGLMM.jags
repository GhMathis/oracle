data {
  D <- dim(Y)
  c <- 1000
}

model {
	
  for (i in 1:D[1]) {    # For each OTU
		for (j in 1:group[1]){ # I both group we test
		
			
			Y[i,j] ~ dpois(exp(phi[i,j]+log(N[j]) -pi[i,j]*c)) 
			phi[i,j] ~ dnorm(alpha[i] , binTau[i]) # random effect
			pi[i,j] ~ dbern(p[i]) # zero inflation
	  	  }
    for (j in (group[1]+1):(group[1]+group[2])){

			Y[i,j] ~ dpois(exp(phi[i,j]+log(N[j]) -pi[i,j]*c))
			phi[i,j] ~ dnorm(alpha[i] +beta[i], binTau[i])
			pi[i,j] ~ dbern(p[i])
		    }
		
    
		binVar[i] ~ dgamma(kappa,eta) T(0,10000)	# Gamma distribution on variance
		
		binTau[i] <- 1/binVar[i]
		alpha[i] ~ dnorm(0,0.01) # OTU specific abundance (intercept)
		beta[i] ~ dnorm(0,0.01) # OTU sepcific difference 
		
		#Same as qin2010
		p[i] ~ dbeta(0.2,1.5) # Zero infaltion parameter specific to OTU (with beta prior distrib)
	    }
	
	kappa ~ dunif(0,50) 
	eta ~ dunif(0,1000) 
	
	
  }